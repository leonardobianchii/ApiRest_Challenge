-- ===== V4: Auditoria DML (idempotente) =====

BEGIN
  EXECUTE IMMEDIATE '
    CREATE TABLE T_AUDITORIA_DML (
      id_auditoria NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
      nm_tabela    VARCHAR2(60)  NOT NULL,
      nm_usuario   VARCHAR2(128) NOT NULL,
      tp_operacao  VARCHAR2(10)  NOT NULL,
      dt_evento    TIMESTAMP     DEFAULT SYSTIMESTAMP,
      ds_old       CLOB,
      ds_new       CLOB
    )';
EXCEPTION WHEN OTHERS THEN IF SQLCODE != -955 THEN RAISE; END IF;
END;
/

CREATE OR REPLACE TRIGGER TRG_AUD_T_CM_ALUGUEL
AFTER INSERT OR UPDATE OR DELETE ON T_CM_ALUGUEL
FOR EACH ROW
DECLARE
  v_json_old CLOB;
  v_json_new CLOB;
  v_op VARCHAR2(10);

  FUNCTION JNUM(n NUMBER) RETURN VARCHAR2 IS
  BEGIN RETURN CASE WHEN n IS NULL THEN 'null' ELSE TO_CHAR(n) END; END;
  FUNCTION JTS(t TIMESTAMP) RETURN VARCHAR2 IS
  BEGIN RETURN CASE WHEN t IS NULL THEN 'null' ELSE '"'||TO_CHAR(t,'YYYY-MM-DD"T"HH24:MI:SS')||'"' END; END;
BEGIN
  IF INSERTING THEN v_op := 'INSERT';
  ELSIF UPDATING THEN v_op := 'UPDATE';
  ELSE v_op := 'DELETE'; END IF;

  IF NOT INSERTING THEN
    v_json_old := '{'||'"id_aluguel":'||JNUM(:OLD.id_aluguel)||','||
                        '"id_cliente":'||JNUM(:OLD.id_cliente)||','||
                        '"id_moto":'||JNUM(:OLD.id_moto)||','||
                        '"dt_retirada":'||JTS(:OLD.dt_retirada)||','||
                        '"dt_devolucao":'||JTS(:OLD.dt_devolucao)||'}';
  END IF;

  IF NOT DELETING THEN
    v_json_new := '{'||'"id_aluguel":'||JNUM(:NEW.id_aluguel)||','||
                        '"id_cliente":'||JNUM(:NEW.id_cliente)||','||
                        '"id_moto":'||JNUM(:NEW.id_moto)||','||
                        '"dt_retirada":'||JTS(:NEW.dt_retirada)||','||
                        '"dt_devolucao":'||JTS(:NEW.dt_devolucao)||'}';
  END IF;

  INSERT INTO T_AUDITORIA_DML (nm_tabela,nm_usuario,tp_operacao,dt_evento,ds_old,ds_new)
  VALUES ('T_CM_ALUGUEL', SYS_CONTEXT('USERENV','SESSION_USER'), v_op, SYSTIMESTAMP, v_json_old, v_json_new);
END;
/
